<?xml version='1.0'?>

<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="1.0"
  xmlns:exsl="http://exslt.org/common"
  xmlns:str="http://exslt.org/strings"
  extension-element-prefixes="exsl str"
>

  <xsl:output method="text" encoding="UTF-8"/>

  <!-- JSON Escaped Strings -->
  <!-- stolen from PreTeXt -->
  <xsl:template name="escape-json-string">
    <xsl:param name="text"/>
    <xsl:variable name="sans-backslash" select="str:replace($text,           '\',      '\\'     )"/>
    <xsl:variable name="sans-slash"     select="str:replace($sans-backslash, '/',      '\/'     )"/>
    <xsl:variable name="sans-quote"     select="str:replace($sans-slash,     '&#x22;', '\&#x22;')"/>
    <xsl:variable name="sans-tab"       select="str:replace($sans-quote,     '&#x09;', '\t'     )"/>
    <xsl:variable name="sans-newline"   select="str:replace($sans-tab,       '&#x0a;', '\n'     )"/>
    <xsl:variable name="sans-return"    select="str:replace($sans-newline,   '&#x0d;', '\r'     )"/>
    <xsl:value-of select="$sans-return" />
  </xsl:template>
  
  <!-- Basic Jupyter Cell -->
  <xsl:template name="jupyter-cell">
    <xsl:param name="source"/>
    <xsl:param name="editable"/>
    <xsl:param name="first-cell"/>
    <xsl:if test="not($first-cell)">,</xsl:if>
    {
     "cell_type": "markdown",
     "metadata": {
      "collapsed": false,
      "editable": <xsl:choose><xsl:when test="$editable"><xsl:value-of select="normalize-space($editable)"/></xsl:when><xsl:otherwise>false</xsl:otherwise></xsl:choose>
     },
    "source": 
    <xsl:choose>
      <xsl:when test="$editable">
        "&lt;div style=\"margin:1em;color:#ccc;font-size:2em;text-align:center;\"&gt;Enter your response here.&lt;/div&gt;"
      </xsl:when>
      <xsl:otherwise>
        "&lt;div style=\"background-color:#f8f8f8;padding:1em;border-radius:10px;box-shadow:4px 4px 3px #ddd;margin:5px;\"&gt;\n\n<xsl:call-template name="escape-json-string"><xsl:with-param name="text" select="normalize-space($source)"/></xsl:call-template>\n\n&lt;/div&gt;"
      </xsl:otherwise>
    </xsl:choose>
    }
  </xsl:template>

  <!-- Jupyter JSON object -->
  <xsl:template match="/">
    {"cells": [
      <xsl:call-template name="jupyter-cell">
        <xsl:with-param name="first-cell">true</xsl:with-param>
        <xsl:with-param name="source">
          &lt;style&gt;.sidebyside{display:flex}.sidebyside *{flex-grow:1;padding-right:1em}.table{width:100% !IMPORTANT}caption{caption-side:top}&lt;/style&gt;
          &lt;h1 class="foo"&gt;1.1 <xsl:value-of select="section/title"/>&lt;/h1&gt;
        </xsl:with-param>
      </xsl:call-template>
      <xsl:apply-templates select="//activity|//exploration"/>
    ]}
  </xsl:template>


  <!-- Supported PreTeXt elements -->

  <xsl:template match="exploration|activity">
    <!--xsl:call-template name="jupyter-cell">
      <xsl:with-param name="source">
        &lt;hr&gt;
      </xsl:with-param>
    </xsl:call-template>-->
    <xsl:call-template name="jupyter-cell">
      <xsl:with-param name="source">
        ## <xsl:if test="local-name()='exploration'">Preview </xsl:if>
        Activity 1.1.<xsl:value-of select="position()"/>
      </xsl:with-param>
    </xsl:call-template>
    <xsl:apply-templates select="statement"/>
  </xsl:template>

  <xsl:template match="statement">
    <xsl:apply-templates select="*"/>
  </xsl:template>

  <xsl:template match="p">
    <xsl:choose>
      <xsl:when test="text()[normalize-space()]">
        <xsl:call-template name="jupyter-cell">
          <xsl:with-param name="source">
            <xsl:apply-templates select="." mode="markdown"/>
          </xsl:with-param>
        </xsl:call-template>
      </xsl:when>
      <xsl:otherwise>
        <xsl:apply-templates select="*"/>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>
  <xsl:template match="p" mode="markdown">
    <xsl:apply-templates select="m|text()"/>
  </xsl:template>

  <xsl:template match="sidebyside">
    <xsl:call-template name="jupyter-cell">
      <xsl:with-param name="source">
        &lt;div class="sidebyside"&gt;
          <xsl:apply-templates select="*" mode="markdown"/>
        &lt;/div&gt;
      </xsl:with-param>
    </xsl:call-template>
  </xsl:template>

  <xsl:template match="ol|ul">
    <xsl:apply-templates select="*"/>
  </xsl:template>

  <xsl:template match="li">
    <xsl:call-template name="jupyter-cell">
      <xsl:with-param name="source">
        &lt;b&gt;<xsl:number format="a. "/>&lt;/b&gt;
        <xsl:apply-templates select="*[position()=1]" mode="markdown"/>
      </xsl:with-param>
    </xsl:call-template>
    <xsl:apply-templates select="*[position()>1]"/>
    <xsl:call-template name="jupyter-cell">
      <xsl:with-param name="editable">true</xsl:with-param>
    </xsl:call-template>
  </xsl:template>

  <xsl:template match="image|figure|table">
    <xsl:call-template name="jupyter-cell">
      <xsl:with-param name="source">
        <xsl:apply-templates select="." mode="markdown"/>
      </xsl:with-param>
    </xsl:call-template>
  </xsl:template>
  <xsl:template match="image" mode="markdown">
    &lt;div&gt;&lt;img src="apc/src/<xsl:value-of select="@source"/>.svg"/&gt;&lt;/div&gt;
  </xsl:template>
  <xsl:template match="figure" mode="markdown">
    &lt;div&gt;&lt;figure&gt;&lt;figcaption&gt;<xsl:apply-templates select="caption"/>&lt;/figcaption&gt;<xsl:apply-templates select="image" mode="markdown"/>&lt;/figure&gt;&lt;/div&gt;
  </xsl:template>
  <xsl:template match="table" mode="markdown">
    &lt;div&gt;&lt;table&gt;&lt;caption&gt;<xsl:apply-templates select="caption"/>&lt;/caption&gt;<xsl:apply-templates select="tabular/row"/>&lt;/table&gt;&lt;/div&gt;
  </xsl:template>

  <xsl:template match="row">
    &lt;tr&gt;<xsl:apply-templates select="cell"/>&lt;/tr&gt;
  </xsl:template>

  <xsl:template match="cell">
    &lt;td&gt;
      <xsl:call-template name="escape-json-string">
        <xsl:with-param name="text" select="normalize-space(.)"/>
      </xsl:call-template>
    &lt;/td&gt;
  </xsl:template>

  <xsl:template match="m"> \(<xsl:call-template name="escape-json-string"><xsl:with-param name="text" select="normalize-space(.)"/></xsl:call-template>\) </xsl:template>

  <xsl:template match="text()|caption"><xsl:call-template name="escape-json-string"><xsl:with-param name="text" select="normalize-space(.)"/></xsl:call-template></xsl:template>

</xsl:stylesheet>